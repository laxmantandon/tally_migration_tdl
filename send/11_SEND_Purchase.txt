;; Shivgoraksh


[Collection: SEND_PurchaseInvoice]
		
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.voucher'

	RemoteRequest: SEND_PurchaseInvoice : UTF8
	JSON Object Path: "message:1"
	
;[#Menu: ExpressIntegration]
;	
;;	Add: Item : Send PurchaseInvoice : Call: SEND_PurchaseInvoice
;	Add: Item: Send PurchaseInvoice Cont: Call: SEND_PurchaseInvoice_Start

	[Function: SEND_PurchaseInvoice_Start]
		
		10 : Start Timer: SEND_PurchaseInvoice_Timer : 1
		
	[System: Events]
		
		SEND_PurchaseInvoice_Timer : Timer: True: Call: SEND_PurchaseInvoice


[Function: SEND_PurchaseInvoice]

	Variable : vName  : String
	Variable: vMessage : String
	Variable: vDocName : String
	Variable: vStatus : Logical

	06 : Do If: ($$NumItems:SEND_PurchaseInvoice_Tally = 0) :  Stop Timer: SEND_PurchaseInvoice_Timer
	07 : Do If: ($$NumItems:SEND_PurchaseInvoice_Tally = 0) :  Return

	08 : Stop Timer: SEND_PurchaseInvoice_Timer
	09 : Start Progress: 1 : "Sending Purchase Invoices" : ""

	10 : Walk Collection: SEND_PurchaseInvoice
	11 : Set: vStatus : $Status
	20 : 	If: $Status
	21 :	Start Batch Post
	25 : 		Walk Collection: data
	26 : 		Log: $name
	28 : 		Set: vName : "ID:"+$name
	28a:		Set: vDocName : $docname
	29 : 		Set: vMessage : $message
	30 : 			Modify Object: (Voucher, ##vName).WebStatus[1].WebStatus : @@ERPNextResetFlag
	32 : 			Modify Object: (Voucher, ##vName).WebStatus_Message[1].WebStatus_Message : ##vMessage
	33 : 			Modify Object: (Voucher, ##vName).WebStatus_DocName[1].WebStatus_DocName : ##vDocName
	40 :		End Walk
	42 :	End Batch Post
	45 : 	End If
	60 : End Walk
	
	65 : Do If: ($$NumItems:SEND_PurchaseInvoice_Tally > 0 and Not $$IsEmpty:##vStatus) :  Start Timer: SEND_PurchaseInvoice_Timer : 1

;[#Menu: Gateway of Tally]
;	
;	Add: PurchaseInvoice:TEst1 : Display: SEND_PurchaseInvoice

[Report : SEND_PurchaseInvoice]
	
	Form: SEND_PurchaseInvoice
	
	[Form: SEND_PurchaseInvoice]
		
		Part: SEND_PurchaseInvoice
		Delete: XMLTag

	[Part: SEND_PurchaseInvoice]
		
		Line: SEND_PurchaseInvoice
		Repeat: SEND_PurchaseInvoice : SEND_PurchaseInvoice_Tally
		Scroll: Vertical
		
		[Line: SEND_PurchaseInvoice]
			;PI_debit_to
			Field: PI_doctype, PI_naming_series, PI_supplier, PI_company, PI_posting_date,PI_posting_time, PI_due_date, PI_currency
			Field: PI_gst_category, PI_remarks, PI_webstatus_docname, PI_tally_masterid, PI_vouchernumber
			Field: PI_set_posting_time
			Field: PI_is_return,PI_bill_no, PI_bill_date;, PI_tally_voucher_no
			JSONTag: "data"
			Empty: $$Line > @@RecordsLimit	
			Explode: PI_items
			Explode: PI_ledgers

			[Field: PI_doctype]
				
				Set as: "Purchase Invoice"
				JSONTag: "doctype"

			[Field: PI_naming_series]
				
				Set as: @@ERPNextNamingSeries
				JSONTag: "naming_series"

			[Field: PI_supplier]
				
				Set as:$PartyLedgerName
				JSONTag: "supplier"
				
			[Field: PI_company]
				
				Set as: @@ERPNextBranch
				JSONTag: "company"

			[Field: PI_posting_date]
				
				Set as: $$GetSqlDate:$Date
				JSONTag: "posting_date"

			[Field: PI_posting_time]
				
				Set as: $$MachineTime
				JSONTag: "posting_time"

			[Field: PI_due_date]
				
				Set as: $$GetSqlDate:@DueDate
				JSONTag: "due_date"
				DueDate : if $$IsEmpty:@AllocDate then $Date + 1 else @AllocDate + 1 
				AllocDate : $LedgerEntries[1,@@IsDebtorCreditor].BillAllocations[1].BillCreditPeriod

			[Field: PI_currency]
				
				Set as: "INR"
				JSONTag: "currency"

			[Field: PI_base_total]
				
				Type: Number
				Set as: $$Number:$Amount
				JSONTag: "base_total"

			[Field: PI_gst_category]
				
				Set as: if $GSTRegistrationType = "Regular" then "Registered Regular" else + 
						If $GSTRegistrationType = "Composition" then "Registered Composition" else + 
						If $GSTRegistrationType = "Consumer" then "Consumer" else + 
						If $GSTRegistrationType = "Unregistered" then "Unregistered" else ""
						
				JSONTag : "gst_category"

			[Field: PI_debit_to]
				
				Set as: $PartyLedgerName + " - " + @@EnableERPNextCmpAbbr
				JSONTag: "credit_to"
				Invisible: Yes

			[Field: PI_remarks]
				
				Set as: $Narration
				JSONTag: "remarks"

			[Field: PI_webstatus_docname]
				
				Set as: if $$IsEmpty:$WebStatus_DocName then "NA" else $WebStatus_DocName
				JSONTag: "webstatus_docname"
				
			[Field: PI_tally_masterid]
				
				Set as: $masterid
				JSONTag: "tally_masterid"

			[Field: PI_vouchernumber]
				
				Set as: $vouchernumber
				JSONTag: "tally_voucherno"
			
			[Field: PI_set_posting_time]
				
				Set as: 1
				JSONTag: "set_posting_time"

			[Field: PI_is_return]
				
				Set as: If $$IsDebitNote:$VoucherTypeName then 1 else 0
				JSONTag: "is_return"
				Invisible: Yes
				
			[Field: PI_bill_no]
				
				JSONTag: "bill_no"
				Set as: $Reference

			[Field: PI_bill_date]
				
				JSONTag: "bill_date"
				Set as: $$GetSqlDate:$ReferenceDate

;			[Field: PI_tally_voucher_no]
;				
;				Set as: $VoucherNumber
;				JSONTag: "tally_voucher_no"
				


	[Part: PI_items]
		
		Line: PI_items 
		Repeat: PI_items  : InventoryEntries
		Scroll: Vertical
				
		[Line: PI_items]
			
			Field: PI_item_code, PI_item_name, PI_description, PI_gst_hsn_code, PI_item_group, PI_qty, PI_stock_qty, PI_uom, PI_stock_uom
			Field: PI_conversion_factor, PI_rate, PI_base_rate, PI_stock_uom_rate
			Field: PI_amount, PI_base_amount, PI_expense_account, PI_warehouse, PI_cost_center, PI_item_tax_template
			Field: PI_base_price_list_rate, PI_discount_percentage, PI_price_list_rate
			JSONTag: "items"
			
			[Field: PI_item_code]
				
				JSONTag : "item_code"
				Set as: if $$IsEmpty:$Item_Alias:StockItem:$StockItemName and $$IsEmpty:$partno:StockItem:$StockItemName then $MasterId:StockItem:$StockItemName else if $$IsEmpty:$Item_Alias:StockItem:$StockItemName then $partno:StockItem:$StockItemName else $Item_Alias:StockItem:$StockItemName

			[Field: PI_item_name]
				
				Set as: $StockItemNAme
				JSONTag: "item_name"
				
			[Field: PI_description]

				Set as: $StockItemName
				JSONTag: "description"

			[Field: PI_gst_hsn_code]
				
				JSONTag: "gst_hsn_code"
				Set as	 : @HSNCode
				HSNCode  : $(GSTCLASSIFICATION, @GSTClassification).GSTDetails[Last].HSNCode
				GSTClassification : if $$IsEmpty:($Classification_Name:StockItem:$StockItemName) then $Classification_Name:StockGroup:$Parent:StockItem:$StockItemName else $Classification_Name:StockItem:$StockItemName

			[Field: PI_item_group]
				
				JSONTag: "item_group"
				Set as: $Parent:StockItem:$StockItemName

			[Field: PI_qty]

				Type: Number
				Set as: $BilledQty ;if $$IsDebitNote:$VoucherTypeName then $$Abs:$$Number:$billedQty * -1 else $$Abs:$$Number:$billedQty
				JSONTag: "qty"
				Format: "Decimal:2"

			[Field: PI_stock_qty]
				
				Type: Number
				Set as: $BilledQty ;if $$IsDebitNote:$VoucherTypeName then $$Abs:$$Number:$billedQty * -1 else $$Abs:$$Number:$billedQty
				JSONTag: "stock_qty"
				Format: "Decimal:2"

			[Field: PI_uom]

				Set as: $BaseUnits:StockItem:$StockItemName
				JSONTag: "uom"

			[Field: PI_stock_uom]

				Set as: $BaseUnits:StockItem:$StockItemName
				JSONTag: "stock_uom"

			[Field: PI_conversion_factor]
				
				Type: Number
				Set as: 1
				JSONTag: "conversion_factor"

			[Field: PI_rate]

				Type: Number
				Set as: @LocForm
				LocForm : $$Number:#PI_base_amount / $$Number:$billedQty
				JSONTag: "rate"
				Format: "Decimal:3"

			[Field: PI_base_rate]

				Type: Number
				Set as: $$Number:#PI_base_amount / $$Number:$billedQty
				JSONTag: "base_rate"
				Format: "Decimal:3"

			[Field: PI_stock_uom_rate]

				Type: Number
				Set as: $$Number:#PI_base_amount / $$Number:$billedQty
				JSONTag: "stock_uom_rate"
				Format: "Decimal:3"

			[Field: PI_amount]

				Type: Number
				Set as: $$Number:$Amount
				JSONTag: "amount"
				Format: "Decimal:3"
				

			[Field: PI_base_amount]

				Type: Number
				Set as: $$Number:$Amount
				JSONTag: "base_amount"
				Format: "Decimal:3"

			[Field: PI_expense_account]

				Set as: $AccountingAllocations[1].LedgerName + " - " + @@EnableERPNextCmpAbbr
				JSONTag: "expense_account"

			[Field: PI_warehouse]

				Set as: $BatchAllocations[1].godownName + " - " +@@EnableERPNextCmpAbbr
				JSONTag: "warehouse"

			[Field: PI_cost_center]

				Set as: "Main - " + @@EnableERPNextCmpAbbr
				JSONTag: "cost_center"

			[Field: PI_selling_price_list]

				Set as: if $$IsEmpty:$PriceLevel then "Standard Selling" else $PriceLevel
				JSONTag: "selling_price_list"


			[Field: PI_item_tax_template]
				
				JSONTag: "item_tax_template"
				Set as: If $GSTTransNature contains "Interstate" then +
							"Interstate GST @"+ $$String:@IGSTRate +"% - " + @@EnableERPNextCmpAbbr else +
							"Local GST @"+ $$String:@IGSTRate +"% - " + @@EnableERPNextCmpAbbr
				IGSTRate : $GST_Rate:GSTCLASSIFICATION:@GSTClassification
				GSTClassification : if $$IsEmpty:($Classification_Name:StockItem:$StockItemName) then $Classification_Name:StockGroup:($Parent:StockItem:$StockItemName) else $Classification_Name:StockItem:$StockItemName
		

			[Field: PI_base_price_list_rate]
				
				Type: Number
				JSONTag: "base_price_list_rate"
				Set as: $rate
				
			[Field: PI_discount_percentage]
				
				Type: Number
				JSONTag: "discount_percentage"
				Set as: $discount

			[Field: PI_price_list_rate]

				Type: Number
				JSONTag: "price_list_rate"
				Set as: $rate
				

	[Part: PI_ledgers]
		
		Line: PI_ledgers 
		Repeat: PI_ledgers  : LedgerEntries
		Scroll: Vertical
				
		[Line: PI_ledgers]
			;PILed_rate
			Field: PILed_charge_type, PILed_row_id, PILed_account_head, PILed_tax_base_amount, PILed_base_tax_amount_after_discount_amount ,PILed_description, PILed_cost_center
			Field: PILed_tax_amount, PILed_base_total
			Field: PILed_tax_amount_after_discount_amount
			JSONTag: "taxes"
			Remove if: $LedgerName = $PartyLEdgerName or $$IsEmpty:$Amount or $VatDealerNature:Ledger:$LedgerName = "Invoice Rounding"
			
			[Field: PILed_charge_type]
				
				Set as: @@charge_type
;				"On Net Total"
				JSONTag: "charge_type"

			[Field: PILed_row_id]
				
				Set as: @@row_id
				JSONTag: "row_id"


			[Field: PILed_account_head]
				
				Set as: $LedgerName + " - " + @@EnableERPNextCmpAbbr
 				JSONTag: "account_head"

			[Field:PILed_description]

				Set as: $LedgerName
				JSONTag: "description"
				
			[Field: PILed_rate]
				
				Set as: "0"
				JSONTag: "rate"

			[Field: PILed_cost_center]

				Set as: "Main - " + @@EnableERPNextCmpAbbr
				JSONTag: "cost_center"

			[Field: PILed_tax_amount]
				
				Type: Number
				Set as: $$Number:$amount
				JSONTag: "tax_amount"

			[Field: PILed_tax_base_amount]
				
				Type: Number
				Set as: $$Number:$amount
				JSONTag: "base_tax_amount"
				
			[Field: PILed_tax_amount_after_discount_amount]
				
				Type: Number
				Set as: $$Number:$amount
				JSONTag: "tax_amount_after_discount_amount"
				
			[Field: PILed_base_tax_amount_after_discount_amount]
				
				Type: Number
				Set as: $$Number:$amount
				JSONTag: "base_tax_amount_after_discount_amount"

			[Field: PILed_base_total]
				
				Type: Number
				Set as: $$CollNumTotal:InventoryEntries:$Amount
				JSONTag: "base_total"
				Format: "Decimal:3"


[Collection: SEND_PurchaseInvoice_Tally]

	Type		: Vouchers : VoucherType
	Child Of	: $$VchTypePurchase
	Belongs To	: Yes
	Filter 		: WebStatus
	Fetch		: VoucherTypeName, PartyLedgerNAme ,Date, Narration, Amount, WebStatus_DocName, Reference, ReferenceDate
	Fetch		: LedgerEntries.LedgerName, LedgerEntries.Amount
	Fetch		: InventoryEntries.StockItemName, InventoryEntries.BilledQty, InventoryEntries.Rate, InventoryEntries.Amount
	Filter		: NonCancelled
	Filter		: NonOptional
	Filter		: NonZeroEntries
	
;	Filter: PURCDeFilter
;	
;	[System: Formula]
;		
;		PURCDeFilter : $Reference = "INVCMH2100083"