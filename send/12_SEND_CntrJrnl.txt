;; Shivgoraksh


[Collection: SEND_Contra]
		
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.voucher'

	RemoteRequest: SEND_Contra : UTF8
	JSON Object Path: "message:1"
	
;[#Menu: ExpressIntegration]
;	
;;	Add: Item : Send Contra : Call: SEND_Contra
;	Add: Item: Send Contra Cont : Call: SEND_Contra_Start

	[Function: SEND_Contra_Start]
		
		10 : Start Timer: SEND_Contra_Timer : 1
		
	[System: Events]
		
		SEND_Contra_Timer : Timer: True: Call: SEND_Contra

[Function: SEND_Contra]

	Variable : vName  : String
	Variable: vMessage : String
	Variable: vDocName : String
	Variable: vStatus : Logical

	06 : Do If: ($$NumItems:SEND_Contra_Tally = 0) :  Stop Timer: SEND_Contra_Timer
	07 : Do If: ($$NumItems:SEND_Contra_Tally = 0) :  Return

	08 : Stop Timer: SEND_Contra_Timer
	09 : Start Progress: 1 : "Sending Contra and Journal " : ""

	10 : Walk Collection: SEND_Contra
	11 : Set: vStatus: $Status
	20 : 	If: $Status
	21 :	Start Batch Post
	25 : 		Walk Collection: data
	26 : 		Log: $name
	28 : 		Set: vName : "ID:"+$name
	28a:		Set: vDocName : $docname
	29 : 		Set: vMessage : $message
	30 : 			Modify Object: (Voucher, ##vName).WebStatus[1].WebStatus : @@ERPNextResetFlag
	32 : 			Modify Object: (Voucher, ##vName).WebStatus_Message[1].WebStatus_Message : ##vMessage
	33 : 			Modify Object: (Voucher, ##vName).WebStatus_DocName[1].WebStatus_DocName : ##vDocName
	40 :		End Walk
	42 :	End Batch Post
	45 : 	End If
	60 : End Walk
	
	65 : Do If: ($$NumItems:SEND_Contra_Tally > 0 and Not $$IsEmpty:##vStatus) :  Start Timer: SEND_Contra_Timer : 1


;[#Menu: Gateway of Tally]
;	
;	Add: Contra:TEst1 : Display: SEND_Contra

[Report : SEND_Contra]
	
	Form: SEND_Contra
	
	[Form: SEND_Contra]
		
		Part: SEND_Contra
		Delete: XMLTag

	[Part: SEND_Contra]
		
		Line: SEND_Contra
		Repeat: SEND_Contra : SEND_Contra_Tally
		Scroll: Vertical
		
		[Line: SEND_Contra]
			
			Field: CNT_doctype, CNT_naming_series, CNT_voucher_type, CNT_company, CNT_posting_date
			Field: CNT_user_remark
			Field: CNT_MasterId, CNT_webstatus_docname, CNT_tally_voucherno
			Empty: $$Line > @@RecordsLimit
			JSONTag: "data"			
			Explode: CNT_ledgers

			[Field: CNT_doctype]
				
				Set as: "Journal Entry"
				JSONTag: "doctype"

			[Field: CNT_naming_series]
				
				Set as: @@ERPNextNamingSeries
				JSONTag: "naming_series"

			[Field: CNT_voucher_type]
				
				Set as: if $$IsContra:$VoucherTypeName then "Contra Entry" else if $$IsJournal:$VoucherTypeName then "Journal Entry" else "Journal Entry"
				JSONTag: "voucher_type"
				
			[Field: CNT_company]
				
				Set as: @@ERPNextBranch
				JSONTag: "company"

			[Field: CNT_posting_date]
				
				Set as: $$GetSqlDate:$Date
				JSONTag: "posting_date"
				
			[Field: CNT_user_remark]
				
				Set as: $Narration
				JSONTag: "user_remark"

			[Field: CNT_MasterId]
				
				Set as: $MasterID
				JSONTag: "tally_masterid"
		
			[Field: CNT_webstatus_docname]
				
				Set as: if $$IsEmpty:$WebStatus_DocName then "NA" else $WebStatus_DocName
				JSONTag: "webstatus_docname"
				
			[Field: CNT_tally_voucherno]
				
				Set as: $VoucherNumber ;if $$IsEmpty:$WebStatus_DocName then "NA" else $WebStatus_DocName
				JSONTag: "tally_voucherno"

	[Part: CNT_ledgers]
		
		Line: CNT_ledgers 
		Repeat: CNT_ledgers  : LedgerEntries
		Scroll: Vertical
				
		[Line: CNT_ledgers]

			Field: CNT_Account, CNT_account_currency, CNT_cost_center, CNT_exchange_rate
			Field: CNT_debit_in_account_currency, CNT_debit, CNT_credit_in_account_currency, CNT_credit
			Field: CNT_is_advance
			Field: CNT_party_type, CNT_party
			JSONTag: "accounts"
			
			[Field: CNT_Account]

				JSONTag: "account"
				Set as: $LedgerName +  " - " + @@EnableERPNextCmpAbbr

			[Field: CNT_account_currency]

				JSONTag: "account_currency"
				Set as: "INR"

			[Field: CNT_cost_center]
				
				JSONTag: "cost_center"
				Set as: "Main - " + @@EnableERPNextCmpAbbr

			[Field: CNT_exchange_rate]

				JSONTag: "exchange_rate"
				Set as: "1"

			[Field: CNT_debit_in_account_currency]

				Type: Number
				JSONTag: "debit_in_account_currency"
				Set as: if $$IsDr:$Amount then $$Abs:$$Number:$Amount else 0

			[Field: CNT_debit]

				Type: Number
				JSONTag: "debit"
				Set as: if $$IsDr:$Amount then $$Abs:$$Number:$Amount else 0

			[Field: CNT_credit_in_account_currency]

				JSONTag: "credit_in_account_currency"
				Set as: if $$IsDr:$Amount then 0 else $$Abs:$$Number:$Amount

			[Field: CNT_credit]

				JSONTag: "credit"
				Set as: if $$IsDr:$Amount then 0 else $$Abs:$$Number:$Amount

			[Field: CNT_is_advance]

				JSONTag: "is_advance"
				Set as: "No"

			[Field: CNT_party_type]
				
				JSONTag: "party_type"
				Set as: If $$IsLedOfGrp:$LedgerName:$$GroupSundryDebtors then "Customer" else if $$IsLedOfGrp:$LedgerName:$$GroupSundryCreditors then "Supplier" else ""
				
			[Field: CNT_party]

				JSONTag: "party"
				Set as : If ($$IsLedOfGrp:$LedgerName:$$GroupSundryDebtors or $$IsLedOfGrp:$LedgerName:$$GroupSundryCreditors) then $LedgerName else ""


[Collection: SEND_Contra_Tally]

	Collection:  SEND_JVPayment_Tally, SEND_Contra_Tally1, SEND_Journal_Tally, SEND_JVReceipt_Tally
	
[Collection: SEND_Contra_Tally1]

	Type		: Vouchers : VoucherType
	Child Of	: $$VchTypeContra
	Belongs To	: Yes
	Filter 		: WebStatus
	Fetch: VoucherTypeName, PartyLedgerNAme ,Date, Narration, Amount, WebStatus_DocName
	Fetch: LedgerEntries.LedgerName, LedgerEntries.Amount
	Fetch: InventoryEntries.StockItemName, InventoryEntries.BilledQty, InventoryEntries.Rate, InventoryEntries.Amount
	Filter: NonCancelled
	Filter: NonOptional
	Filter: NonZeroEntries

[Collection: SEND_Journal_Tally]

	Type		: Vouchers : VoucherType
	Child Of	: $$VchTypeJournal
	Belongs To	: Yes
	Filter 		: WebStatus
	Fetch: VoucherTypeName, PartyLedgerNAme ,Date, Narration, Amount, WebStatus_DocName
	Fetch: LedgerEntries.LedgerName, LedgerEntries.Amount
	Fetch: InventoryEntries.StockItemName, InventoryEntries.BilledQty, InventoryEntries.Rate, InventoryEntries.Amount
	Filter: NonCancelled
	Filter: NonOptional
	Filter: NonZeroEntries
	
[Collection: SEND_JVPayment_Tally]

	Type		: Vouchers : VoucherType
	Child Of	: $$VchTypePayment
	Belongs To	: Yes
	Filter 		: WebStatus
	Fetch: VoucherTypeName, PartyLedgerNAme ,Date, Narration, Amount, WebStatus_DocName
	Fetch: LedgerEntries.LedgerName, LedgerEntries.Amount
	Fetch: InventoryEntries.StockItemName, InventoryEntries.BilledQty, InventoryEntries.Rate, InventoryEntries.Amount

	Filter: IsExpensePymt
	
	Filter: NonCancelled
	Filter: NonOptional
	Filter: NonZeroEntries


[Collection: SEND_JVReceipt_Tally]

	Type		: Vouchers : VoucherType
	Child Of	: $$VchTypeReceipt
	Belongs To	: Yes
	Filter 		: WebStatus
	Fetch: VoucherTypeName, PartyLedgerNAme ,Date, Narration, Amount, WebStatus_DocName
	Fetch: LedgerEntries.LedgerName, LedgerEntries.Amount
	Fetch: InventoryEntries.StockItemName, InventoryEntries.BilledQty, InventoryEntries.Rate, InventoryEntries.Amount

	Filter: IsExpensePymt
	
	Filter: NonCancelled
	Filter: NonOptional
	Filter: NonZeroEntries

